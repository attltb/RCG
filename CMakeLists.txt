CMAKE_MINIMUM_REQUIRED (VERSION 3.13)

SET (ProjectName "BinaryResourceGenerator")
PROJECT (${ProjectName})


# Common
SET (CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "Configs" FORCE)
SET (CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}" CACHE PATH "Install prefix" FORCE)

if (MSVC)
	SET (CMAKE_USE_RELATIVE_PATHS ON CACHE INTERNAL "" FORCE)
	ADD_COMPILE_OPTIONS("/Zc:__cplusplus")
endif ()

SET (CMAKE_CXX_STANDARD_REQUIRED YES) 
SET (CMAKE_CXX_STANDARD 14)
SET (CMAKE_CXX_EXTENSIONS NO)

if (DEFINED CMAKE_BUILD_TYPE)
	SET_PROPERTY (CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS ${CMAKE_CONFIGURATION_TYPES})
endif ()

MESSAGE (STATUS "Compiler ID: ${CMAKE_CXX_COMPILER_ID}")

if(ENABLE_SOLUTION_FOLDERS)
  SET_PROPERTY (GLOBAL PROPERTY USE_FOLDERS ON)
  SET_PROPERTY (GLOBAL PROPERTY PREDEFINED_TARGETS_FOLDER "CMakeTargets")
endif()


# OpenCV related
SET (OpenCV_REQUIRED_PACKAGES "core;imgcodecs;imgproc")
MESSAGE (STATUS "OpenCV required packages: ${OpenCV_REQUIRED_PACKAGES}")
FIND_PACKAGE(OpenCV REQUIRED ${OpenCV_REQUIRED_PACKAGES})
INCLUDE_DIRECTORIES ( ${OpenCV_INCLUDE_DIRS} )

if(MSVC)
	if("${OpenCV_STATIC}" STREQUAL "ON" OR "${OpenCV_SHARED}" STREQUAL "OFF")
		MESSAGE (STATUS "Setting static runtime for MSVC")
		foreach (flag_var CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS_MINSIZEREL CMAKE_CXX_FLAGS_RELWITHDEBINFO)
			STRING (REGEX REPLACE "/MD" "/MT" ${flag_var} "${${flag_var}}")
		endforeach(flag_var)
	endif()
endif()


# Project specific
SET (RCG_LANGUAGE "Korean" CACHE STRING "Select language")
SET_PROPERTY (CACHE RCG_LANGUAGE PROPERTY STRINGS "Korean" "English")

ADD_DEFINITIONS (-D_CRT_SECURE_NO_WARNINGS)
INCLUDE_DIRECTORIES ("${CMAKE_SOURCE_DIR}/src")

ADD_EXECUTABLE (${ProjectName})

SET_PROPERTY (DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${ProjectName})
TARGET_LINK_LIBRARIES (${ProjectName}  ${OpenCV_LIBS})

SOURCE_GROUP ("Header Files" REGULAR_EXPRESSION "h$|hpp$")
SOURCE_GROUP ("Source Files" REGULAR_EXPRESSION "cc$|cpp$|c$")
SOURCE_GROUP ("Include Files" REGULAR_EXPRESSION "inc$|inc.h$")

ADD_SUBDIRECTORY (src)